apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: 'gradle/dependencies.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath libraries.gradlePlugins.android
        classpath libraries.gradlePlugins.kotlin
        classpath libraries.gradlePlugins.depMgmt
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

dependencyUpdates.resolutionStrategy = {
    ComponentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    distributionUrl = "https://services.gradle.org/distributions/gradle-4.9-all.zip"
}
